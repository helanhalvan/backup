module List_driver
import Agent_passive
import Global_funs
import SDL.SDL_Main
import Ped_util.UI
def list(agents: [Agent],ui:bool) : void {
     let
         i = 0
         drivers= new [List_driver]( |agents| )
         j = 0
         sdl_main = if ui then make_UI() else null : SDL_Main
         manager = new List_manager(40000,ui,sdl_main)
     in
     {
         for a in agents {
             drivers[i]=new List_driver(a,|agents|,a.hash_code,manager);
             i=i+1;
         };
         i=0;
         for a in drivers{
             for b in drivers {
                 unless (a==b) then {
                 a!link(b,get(b.id()));
                 }
             }
         };
        --print("starting!\n");
         manager.start(drivers);
     }
 }
class List_manager {
    i : int
    wait : int
    dr : [List_driver]
    ui : bool
    sdl_main : SDL_Main
    def init(iterations:int,ui:bool,sdl_main:SDL_Main) : void {
        this.i=iterations;
        this.ui=ui;
        this.sdl_main=sdl_main;
    }
    def start(drivers:[List_driver]) : void {
        this.dr=drivers;
        this!move();
    }
    def move() : void {
        this.i=this.i-1;
        if this.i<1 then { () } else {
            if(this.i%10000==0) then print(this.i);
            if(this.ui) then {
                let positions = new [(int,int)](|this.dr|);
                    for w in [0..(|this.dr|-1)] {
                        positions[w]=get((this.dr[w]).get_pos());
                    };
                    show(positions,this.sdl_main)
                };
            this.wait=(|this.dr|);
            for a in this.dr { a!move() }
        }
    }
    def reply() : void {
        this.wait=this.wait-1;
        if this.wait==0 then this.move()
    }
}
class List_driver {
--message passing stuff
    manager : List_manager
    agent : Agent
    links : [List_driver]
    d_list : [int]
    close : [List_driver]
    id : int
--holding while waiting
    index : int
    responses_left : [int]
    full : [bool]
    target_x : [int]
    target_y : [int]
    moved : bool

    def get_pos() : (int,int) {
        this.agent.pos();
    }
    def init(a:Agent,size:int,id:int,manager:List_manager) : void {
        this.manager=manager;
        this.id=id;
        this.agent=a;
        this.links=new [List_driver](size);
        this.close=new [List_driver](size);
        this.full = new [bool](2);
        this.responses_left = new [int](2);
        this.target_x = new [int](2);
        this.target_y = new [int](2);
        this.d_list = new [int](size);
        for i in [0..size-1]
            (this.d_list)[i]=0;
        this.d_list[id]=50000000; --will not collide with self
        this.links[id]=this;
    }
    def link(a:List_driver,id:int) : void {
        this.links[id]=a;
    }
    def id() : int { this.id }

    def fix_close() : int {
        let close_size=0;
        for index in [0..(|this.d_list|-1)] {
            (this.d_list)[index]=(this.d_list)[index]-2;
            if ( ((this.d_list)[index])<1 ) then {
                (this.close)[close_size]=(this.links)[index];
                close_size=close_size+1;
            };
        };
        --print("{}\n",close_size);
        close_size
    }
    def move() : void {
        let i=0;
        let temp = this.agent.next();
        let close_size = this.fix_close();
        this.moved=false;
        --print(close_size);
        for desired in temp {
            this.responses_left[i]= close_size;
            match desired with (x,y) => {
                this.target_x[i]=x;
                this.target_y[i]=y;
            };
            for qq in [0..(close_size-1)] {
                ((this.close)[qq])!request(this,i);
            };
            i=i+1;
        };
        if close_size==0 then {
            this.agent.move(temp[0]);
            this.manager!reply();
        };
        ()
    }
    def request(d:List_driver,reply_index:int) : void {
        d!reply(reply_index,this.agent.x,this.agent.y,this.id)
    }
    def reply(index:int, x:int,y:int,id:int) : void {
        this.responses_left[index]=this.responses_left[index]-1;
        this.d_list[id]=man_distance_int(x,y,this.target_x[index],this.target_y[index])-1;
        --collision check
        if( (x==(this.target_x)[index]) and (y==(this.target_y)[index]) ) then {
            this.full[index]=true;
        };
        if(this.responses_left[index]==0) then {
            if this.full[index]==false and this.moved==false then {
                this.agent.move_int(this.target_x[index],this.target_y[index]);
                this.moved=true;
            };
            --check if last expected message
            let sum=0;
            for a in this.responses_left {
                --print("{}\n",a);
                sum=sum+a;
            };
            --print("replies left {}\n",sum);
            if sum==0 then {this.manager!reply()}; --if true tell manager done()
        }
    }
}
